grammar com.zazuko.experimental.rmdsl.RdfMapping with org.eclipse.xtext.common.Terminals

generate rdfMapping "http://www.zazuko.com/experimental/rmdsl/RdfMapping"

Domainmodel :
    (elements+=Element)*;
  
Element:
    SourceGroup | LogicalSource | Vocabulary | Mapping | Import |
    SourceTypesDefinition |  DatatypesDefinition | LanguageTagDefinition ;

SourceTypesDefinition: {SourceTypesDefinition}
	'source-types' '{'
		(types+=SourceType)*
	'}';

SourceType:
	name=ID 'referenceFormulation' referenceFormulation=STRING;
	
DatatypesDefinition: {DatatypesDefinition}
	'datatypes' '{'
		prefix=Prefix
		(types+=Datatype)*
	'}';
	
Datatype:
	name=ID;
	
LanguageTagDefinition: {LanguageTagDefinition}
	'language-tags' '{'
		(languageTags+=LanguageTag)*
	'}';
	
LanguageTag:
	name=ID;
  
SourceGroup:
    'source-group' name=ID '{'
    	'type' type=[SourceType] ';'
    	('source' source=STRING ';')?
    	(logicalSources+=LogicalSource)*
    '}';
    
LogicalSource:
	'logical-source' name=ID '{'
		('type' type=[SourceType] ';')?
		('source' source=STRING ';')?
		('iterator' iterator=STRING ';')?
		'referenceables' '{'
			(referencables+=Referenceable)*
	    '}'
	'}';

Referenceable:	
    name=ID (value=STRING)?;
    
Vocabulary:	
	'vocabulary' name=ID '{'
    	prefix=Prefix
		('classes' (classes+=RdfClass)* ';')?
	    ('properties' (properties+=RdfProperty)* ';')?	
    '}';
    
Prefix:
	'prefix' label=STRING iri=STRING ';';
    
RdfProperty:
	name=ID;
	
RdfClass:
	name=ID;	
    
Mapping:
	'map' name=ID 'from' source=[LogicalSource|QualifiedName] '{'
		'subject' 'template' pattern=STRING 'with' reference=[Referenceable|QualifiedName]
		 ('types' (subjectTypeMappings+=SubjectTypeMapping)+)?
		 ('properties' (poMappings+=PredicateObjectMapping)+)?
	'}';
	
SubjectTypeMapping:	
	type=[RdfClass|QualifiedName];

PredicateObjectMapping:	
	property=[RdfProperty|QualifiedName] 
	term=ValuedTerm;
	
ValuedTerm:
	ReferenceValuedTerm | TemplateValuedTerm | LinkedResourceTerm;
// TODO: ConstantValuedTerm
	
ReferenceValuedTerm:
	'from' reference=[Referenceable|QualifiedName]
	('with' 'datatype' datatype=[Datatype] | 'with' 'language-tag' languageTag=[LanguageTag])?;

TemplateValuedTerm:
	'template' pattern=STRING 'with' reference=[Referenceable|QualifiedName];
	
LinkedResourceTerm:
	'link' mapping=[Mapping] 'with' reference=[Referenceable|QualifiedName];

Import:
    'import' importedNamespace=QualifiedNameWithWildcard;
    
QualifiedNameWithWildcard:
    QualifiedName '.*'?;

QualifiedName:
    ID ('.' ID)*;
